plugins {
    id 'idea'
    id 'java-gradle-plugin'
    id 'maven-publish'

    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.palantir.git-version' version '0.5.3'
}

gradlePlugin {
    plugins {
        playLess {
            id = 'org.iatoki.play-webjars'
            implementationClass = 'org.iatoki.gradle.play.webjars.PlayWebJarsPlugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.webjars:webjars-locator-core:0.32'
}

group 'org.iatoki'
version gitVersion().substring(1)

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']

    pkg {
        repo = 'releases'
        name = 'gradle-play-webjars'
        userOrg = 'ia-toki'

        version {
            name = project.version
        }
    }
}
